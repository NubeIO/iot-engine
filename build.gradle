plugins {
    id "eclipse"
    id "idea"
    id "org.sonarqube" version "2.6"
    id "net.ltgt.apt" version "0.19" apply false
}

ext {
    vertxVersion = "3.5.3"
    lombokVersion = "1.18.4"
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", project.name
    }
}

configurations {
    providedRuntime
}

allprojects {
    group = 'com.nubeiot'
}

subprojects {
//    onlyIf {
//        !project.hasProperty("container") || !project.getProperties().get("container") == "true"
//    }
    apply plugin: 'maven'
    apply plugin: "java-library-distribution"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"
    apply from: "$rootDir/buildSrc/bundle.gradle"
    apply from: "$rootDir/buildSrc/nexus.gradle"
    apply from: "$rootDir/buildSrc/misc.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
        source = sourceSets.main.java
        classpath = configurations.compile + configurations.compileOnly
        destinationDir = project.file('src/main/generated')
        options.compilerArgs = [
                "-proc:only",
                "-processor", "io.vertx.codegen.CodeGenProcessor",
                "-AoutputDirectory=${project.projectDir}/src/main"
        ]
    }

    sourceSets {
        main {
            java {
                srcDirs += 'src/main/generated'
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
    }

    dependencies {
        compile group: 'io.vertx', name: 'vertx-core', version: "$project.vertxVersion"
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        compileOnly group: 'io.vertx', name: 'vertx-codegen', version: "$project.vertxVersion"
        compileOnly "org.projectlombok:lombok:$project.lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$project.lombokVersion"

        testCompile group: 'io.vertx', name: 'vertx-unit', version: "$project.vertxVersion"
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.5.2'
        testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
        testCompile group: 'com.jayway.restassured', name: 'rest-assured', version: '2.9.0'
        testCompileOnly "org.projectlombok:lombok:$project.lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$project.lombokVersion"
    }

}

apply from: "buildSrc/verification.gradle"
apply from: "buildSrc/javadoc.gradle"
apply from: "buildSrc/dist.gradle"