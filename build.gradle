import com.nubeiot.buildscript.ProjectUtils
import com.nubeiot.buildscript.docker.DockerBuildTask
import com.nubeiot.buildscript.docker.internal.JavaEdition
import com.nubeiot.buildscript.docker.internal.JavaType

plugins {
    id "eclipse"
    id "idea"
    id "org.sonarqube" apply false
    id "net.ltgt.apt" apply false
}

apply from: "buildSrc/dependencies.gradle"

ext {
    baseName = "nubeio"
}

defaultTasks 'clean', 'jooq', 'build', 'uberJar'
group = 'com.nubeiot'

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    repositories project.repos

    configurations {
        providedRuntime
    }

    version = project.version + "-" + project.semanticVersion
    project.ext.computeBaseName = { -> ProjectUtils.computeBaseName(project) }
    project.ext.loadSecretProps = { f -> ProjectUtils.loadSecretProps(project, f) }
}

subprojects {
    apply plugin: "maven"
    apply plugin: "java-library-distribution"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"

    group = ProjectUtils.computeGroup(project)

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compile project.deps.vertx.core
        compile project.deps.jackson.core
        compile project.deps.jackson.databind
        compile project.deps.jackson.datetime
        compile project.deps.logs.slf4j
        compile project.deps.logs.logback

        compileOnly project.deps.vertx.codegen
        compileOnly project.deps.lombok
        annotationProcessor project.deps.lombok

        testCompile project.deps.tests.junit
        testCompile project.deps.tests.jsonAssert
        testCompile project.deps.vertx.junit
        testCompileOnly project.deps.lombok
        testAnnotationProcessor project.deps.lombok
    }

    task dockerBuild(type: DockerBuildTask) {
        javaVersion = ProjectUtils.extraProp(project, "javaVersion", "8u212")
        javaEdition = JavaEdition.valueOf(ProjectUtils.extraProp(project, "javaEdition", JavaEdition.OPENJDK.name()))
        javaType = JavaType.valueOf(ProjectUtils.extraProp(project, "javaType", JavaType.JRE.name()))
        vcsBranch = ProjectUtils.extraProp(project, "vcsBranch", "")
    }

    apply from: "$rootDir/buildSrc/generated.gradle"
    apply from: "$rootDir/buildSrc/javadoc.gradle"
    apply from: "$rootDir/buildSrc/bundle.gradle"
    apply from: "$rootDir/buildSrc/nexus.gradle"
    apply from: "$rootDir/buildSrc/misc.gradle"
}

apply from: "buildSrc/global.gradle"
apply from: "buildSrc/sonar.gradle"
