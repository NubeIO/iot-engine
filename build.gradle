plugins {
    id "eclipse"
    id "idea"
    id "org.sonarqube" version "2.6"
    id "net.ltgt.apt" version "0.19" apply false
}

ext {
    baseName = "nube"
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", project.name
    }
}

group = 'com.nubeiot'

allprojects {
    version = project.version + "-" + project.semanticVersion

    configurations {
        providedRuntime
    }

    repositories {
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
    }

    apply plugin: "eclipse"
    apply plugin: "idea"
}

def static computeGroup(project) {
    if (project.parent == null) {
        return project.group
    }
    def suffix = project.parent == project.rootProject ? "" : ("." + project.parent.name)
    return computeGroup(project.parent) + suffix
}

def static computeBaseName(project) {
    if (project.parent == null) {
        return project.ext.baseName
    }
    return computeBaseName(project.parent) + "-" + project.name
}

subprojects {
    apply plugin: "maven"
    apply plugin: "java-library-distribution"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"

    group = computeGroup(project)

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    apply from: "$rootDir/buildSrc/dependencies.gradle"

    dependencies {
        compile project.deps.vertx.core
        compile project.deps.jackson.core
        compile project.deps.jackson.databind
        compile project.deps.logs.slf4j
        compile project.deps.logs.logback
        compile project.deps.tests.jsonAssert

        compileOnly project.deps.vertx.codegen
        compileOnly project.deps.lombok
        annotationProcessor project.deps.lombok

        testCompile project.deps.tests.junit
        testCompile project.deps.vertx.junit
        testCompileOnly project.deps.lombok
        testAnnotationProcessor project.deps.lombok
    }

    apply from: "$rootDir/buildSrc/generated.gradle"
    apply from: "$rootDir/buildSrc/javadoc.gradle"
    apply from: "$rootDir/buildSrc/bundle.gradle"
    apply from: "$rootDir/buildSrc/nexus.gradle"
    apply from: "$rootDir/buildSrc/misc.gradle"
}

apply from: "buildSrc/global.gradle"
