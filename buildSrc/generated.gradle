import java.nio.file.Paths

task annotationProcessing(type: JavaCompile, group: 'build') {
    onlyIf {
        project.hasProperty('generated') && project.generated == "true"
    }
    def outputDir = file Paths.get(project.projectDir.absolutePath, project.genParentSrcFolder)
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file(project.genSrcFolder)
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-Acodegen.output=${outputDir.absolutePath}"
    ]
}
compileJava.dependsOn(annotationProcessing)

task generateServiceDescriptor(group: 'build') {
    onlyIf {
        project.hasProperty('serviceDescriptor')
    }
    doLast {
        if (project.serviceDescriptor.trim() == "" || project.mainVerticle.trim() == "") {
            throw new TaskExecutionException(project.tasks.processResources,
                    new RuntimeException("Missing mainVerticle or serviceDescriptor"))
        }
        def resourcesDir = sourceSets.main.output.resourcesDir
        resourcesDir.mkdirs()
        def contents = "{ \"main\": \"$project.mainVerticle\" }"
        new File(resourcesDir, project.serviceDescriptor + ".json").text = contents
    }
}

processResources.dependsOn(generateServiceDescriptor)

sourceSets {
    main {
        java {
            srcDirs += project.genSrcFolder
        }
    }
}